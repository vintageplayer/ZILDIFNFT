scilla_version 0

(***************************************************)
(*               Associated library                *)
(***************************************************)
import IntUtils BoolUtils
library DecentralizedNFTIndex

let zero = Int128 0
let qa = Uint128 1000000000000


let one_msg = 
  fun (msg : Message) => 
  let nil_msg = Nil {Message} in
  Cons {Message} msg nil_msg

let two_msgs =
fun (msg1 : Message) =>
fun (msg2 : Message) =>
  let msgs_tmp = one_msg msg2 in
  Cons {Message} msg1 msgs_tmp

(* Error events *)
type Error =
| CodeIsSender
| CodeInsufficientFunds
| CodeInsufficientAllowance
| CodeInsufficientTokenFunds
| CodeNotContractOwner

let accepted_code = Int32 1

let make_error =
  fun (result : Error) =>
    let result_code = 
      match result with
      | CodeIsSender              => Int32 -1
      | CodeInsufficientFunds     => Int32 -2
      | CodeInsufficientAllowance => Int32 -3
      | CodeInsufficientTokenFunds => Int32 -4
      | CodeNotContractOwner => Int32 -5
      end
    in
    { _exception : "Error"; code : result_code }
  
let zero = Uint128 0
let one = Uint128 1

let get_val =
  fun (some_val: Option Uint128) =>
  match some_val with
  | Some val => val
  | None => zero
  end

(***************************************************)
(*             The contract definition             *)
(***************************************************)

contract FungibleFundToken
(
  contract_owner: ByStr20,
  name : String,
  symbol: String,
  decimals: Uint32,
  init_price: Uint128
)

(* Mutable fields *)
(* Price at which a new token can be minted *)
field token_price: Uint128 = init_price

(* Total number of tokens in supply currently *)
field token_in_supply: Uint128 = zero


(* Number of token holders *)
field num_of_participants: Uint128 = zero

(* Number of tokens held by a user. Let it be an empty map in the beginning. *)
field balances: Map ByStr20 Uint128 = Emp ByStr20 Uint128

(* List of NFTs held by the fund currently from a given nft address*)
field nft_list: Map ByStr20 (Map Uint256 Uint128) = Emp ByStr20 (Map Uint256 Uint128)

(* Total Funds Parked in NFTs *)
field funds_in_assets: Uint128 = zero
field allow_token_sell: Uint128 = one

field allowances: Map ByStr20 (Map ByStr20 Uint128) 
  = Emp ByStr20 (Map ByStr20 Uint128) 
  


(**************************************)
(*             Procedures             *)
(**************************************)

procedure ThrowError(err : Error)
  e = make_error err;
  throw e
end

procedure IsNotSender(address: ByStr20)
  is_sender = builtin eq _sender address;
  match is_sender with
  | True =>
    err = CodeIsSender;
    ThrowError err
  | False =>
  end
end

procedure IsContractOwner()
  is_contract_owner = builtin eq contract_owner _sender;
  match is_contract_owner with
  | True => 
  | False =>
    err = CodeNotContractOwner;
    ThrowError err
  end
end

procedure AuthorizedMoveIfSufficientBalance(from: ByStr20, to: ByStr20, amount: Uint128)
  o_from_bal <- balances[from];
  bal = get_val o_from_bal;
  can_do = uint128_le amount bal;
  match can_do with
  | True =>
    (* Subtract amount from from and add it to to address *)
    new_from_bal = builtin sub bal amount;
    balances[from] := new_from_bal;
    (* Adds amount to to address *)
    get_to_bal <- balances[to];
    new_to_bal = match get_to_bal with
    | Some bal => builtin add bal amount
    | None => amount
    end;
    balances[to] := new_to_bal
  | False =>
    (* Balance not sufficient *)
    err = CodeInsufficientFunds;
    ThrowError err
  end
end

procedure CanUserDestroyFundTokens(amount: Uint128, from: ByStr20)
  bal_from_map <- balances[from];
  bal = get_val bal_from_map;
  can_do = uint128_le amount bal;
  match can_do with
  | False => 
    err = CodeInsufficientTokenFunds;
    ThrowError err
  | True =>
    
  end
end 
    

(***************************************)
(*             Transitions             *)
(***************************************)


(* @Dev: Transition will be used for tranfering ZIL into the fund, the customer will get tokens back instead  *)
transition addFunds()
  accept;
  user_address = _sender;
  user_tokens = _amount;
  tok_price <- token_price;
  fund_tokens_to_send = builtin div _amount tok_price;
  old_balance_from_bal <- balances[user_address];
  old_balance = get_val old_balance_from_bal;
  new_balance = builtin add old_balance fund_tokens_to_send;
  balances[user_address] := new_balance
  (* Send Fee to Marketplace smart contract *)
end

(* @Dev will be used for customer to pull money out of the fund where there is liquidity present  *)
transition removeFunds(amount: Uint128)
  can_sell_token <- allow_token_sell;
  can_sell = builtin eq can_sell_token zero;
  match can_sell with
    | False =>
      err = CodeInsufficientAllowance;
      ThrowError err
    | True =>
      tok_price <- token_price;
      qa_to_send = builtin mul amount tok_price;
      CanUserDestroyFundTokens amount _sender;
      old_balance_from_bal <- balances[_sender];
      old_balance = get_val old_balance_from_bal;
      new_balance = builtin sub old_balance amount;
      balances[_sender] := new_balance;
      msg  = {_tag : ""; _recipient : _sender; _amount : qa_to_send};
      msgs = one_msg msg;
      send msgs
    end
end

transition buyNFT(value: Uint128, nft_address: ByStr20, nft_token_id: Uint256)
  IsContractOwner;
  (*check if there are enough zil to buy nft  *)
  balance <- _balance;
  balance_greater = builtin lt value balance;
  balance_equal = builtin eq value balance;
  have_sufficient_balance = orb balance_greater balance_equal ;
  (*transfer nft*)
  match have_sufficient_balance with
    | False =>
      err = CodeInsufficientFunds;
      ThrowError err
    | True =>
      (* add nft_address & token_id to list *)
      nft_list[nft_address][nft_token_id] := value;
      old_funds_in_assets <- funds_in_assets;
      new_funds_in_assets = builtin add value old_funds_in_assets;
      funds_in_assets := new_funds_in_assets;
      msg  = {
      _tag : "TransferFrom"; 
      _recipient : nft_address;
      _amount : Uint128 0;
      to : _this_address;
      token_id: nft_token_id
    };
    msgs = one_msg msg;
    send msgs;

  (*  transfer zil to the fund manager *)
    msg  = {_tag : ""; _recipient : _sender; _amount : value};
    msgs = one_msg msg;
    send msgs
  end
  (* Invoke CallBack to generate tokens for the users in the pool based on the bonding curve formula*)
end

transition sellNFT(value: Uint128, nft_address: ByStr20, nft_token_id: Uint128)
  IsContractOwner;
  (* give NFT back *)
  msg  = {
    _tag : "Transfer"; 
    _recipient : nft_address;
    _amount : Uint128 0;
    to : _sender;
    token_id: nft_token_id
  };
  msgs = one_msg msg;
  send msgs;
  (* take ZIL  *)
  accept;
  (* Send Fees from the sell value to the marketplace contract *)
  old_funds_in_assets <- funds_in_assets;
  new_funds_in_assets = builtin sub old_funds_in_assets value;
  funds_in_assets := new_funds_in_assets
  (* check the change of the NFT's value in percentage  *)
  (* what is the percentage change in the value of the fund  *)
  (* modify the fund's initial price accordingly  *)
end

transition SetTokenPrice(price: Uint128)
  IsContractOwner;
  token_price := price
end


transition ToggleTokenSell()
  IsContractOwner;
  can_sell_token <- allow_token_sell;
  can_sell = builtin eq can_sell_token zero;
  match can_sell with
    | True =>
      allow_token_sell := one
    | False =>
      allow_token_sell := zero
  end
end

(* @dev: Increase the allowance of an approved_spender over the caller tokens. Only token_owner allowed to invoke.   *)
(* param spender:      Address of the designated approved_spender.                                                   *)
(* param amount:       Number of tokens to be increased as allowance for the approved_spender.                       *)
transition IncreaseAllowance(spender: ByStr20, amount: Uint128)
  IsNotSender spender;
  some_current_allowance <- allowances[_sender][spender];
  current_allowance = get_val some_current_allowance;
  new_allowance = builtin add current_allowance amount;
  allowances[_sender][spender] := new_allowance;
  e = {_eventname : "IncreasedAllowance"; token_owner : _sender; spender: spender; new_allowance : new_allowance};
  event e
end

(* @dev: Decrease the allowance of an approved_spender over the caller tokens. Only token_owner allowed to invoke. *)
(* param spender:      Address of the designated approved_spender.                                                 *)
(* param amount:       Number of tokens to be decreased as allowance for the approved_spender.                     *)
transition DecreaseAllowance(spender: ByStr20, amount: Uint128)
  IsNotSender spender;
  some_current_allowance <- allowances[_sender][spender];
  current_allowance = get_val some_current_allowance;
  new_allowance =
    let amount_le_allowance = uint128_le amount current_allowance in
      match amount_le_allowance with
      | True => builtin sub current_allowance amount
      | False => zero
      end;
  allowances[_sender][spender] := new_allowance;
  e = {_eventname : "DecreasedAllowance"; token_owner : _sender; spender: spender; new_allowance : new_allowance};
  event e
end

(* @dev: Moves an amount tokens from _sender to the recipient. Used by token_owner. *)
(* @dev: Balance of recipient will increase. Balance of _sender will decrease.      *)
(* @param to:  Address of the recipient whose balance is increased.                 *)
(* @param amount:     Amount of tokens to be sent.                                  *)
transition Transfer(to: ByStr20, amount: Uint128)
  AuthorizedMoveIfSufficientBalance _sender to amount;
  e = {_eventname : "TransferSuccess"; sender : _sender; recipient : to; amount : amount};
  event e;
  (* Prevent sending to a contract address that does not support transfers of token *)
  msg_to_recipient = {_tag : "RecipientAcceptTransfer"; _recipient : to; _amount : zero; 
                      sender : _sender; recipient : to; amount : amount};
  msg_to_sender = {_tag : "TransferSuccessCallBack"; _recipient : _sender; _amount : zero; 
                  sender : _sender; recipient : to; amount : amount};
  msgs = two_msgs msg_to_recipient msg_to_sender;
  send msgs
end

(* @dev: Move a given amount of tokens from one address to another using the allowance mechanism. The caller must be an approved_spender. *)
(* @dev: Balance of recipient will increase. Balance of token_owner will decrease.                                                        *)
(* @param from:    Address of the token_owner whose balance is decreased.                                                                 *)
(* @param to:      Address of the recipient whose balance is increased.                                                                   *)
(* @param amount:  Amount of tokens to be transferred.                                                                                    *)
transition TransferFrom(from: ByStr20, to: ByStr20, amount: Uint128)
  o_spender_allowed <- allowances[from][_sender];
  allowed = get_val o_spender_allowed;
  can_do = uint128_le amount allowed;
  match can_do with
  | True =>
    AuthorizedMoveIfSufficientBalance from to amount;
    e = {_eventname : "TransferFromSuccess"; initiator : _sender; sender : from; recipient : to; amount : amount};
    event e;
    new_allowed = builtin sub allowed amount;
    allowances[from][_sender] := new_allowed;
    (* Prevent sending to a contract address that does not support transfers of token *)
    msg_to_recipient = {_tag: "RecipientAcceptTransferFrom"; _recipient : to; _amount: zero; 
                        initiator: _sender; sender : from; recipient: to; amount: amount};
    msg_to_sender = {_tag: "TransferFromSuccessCallBack"; _recipient: _sender; _amount: zero; 
                    initiator: _sender; sender: from; recipient: to; amount: amount};
    msgs = two_msgs msg_to_recipient msg_to_sender;
    send msgs
  | False =>
    err = CodeInsufficientAllowance;
    ThrowError err
  end
end

(* required as per token transfers *)
transition TransferFromSuccessCallBack(from: ByStr20, recipient : ByStr20, token_id: Uint256)
end 

transition RecipientAcceptTransferFrom(from: ByStr20, recipient : ByStr20, token_id: Uint256)
end

transition incentiviseBehaviourCallBack()
end
